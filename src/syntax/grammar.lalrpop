use lalrpop_util::ParseError;

use crate::syntax::token::Token;
use crate::error::SyntaxError;
use crate::types::Spanned;
use crate::syntax::ast;

grammar<'err>(
    errors: &'err mut Vec<ParseError<usize, Token, Spanned<SyntaxError>>>
);

Spanned<T> : ast::Spanned<T> = <l: @L> <t: T> <r: @R> => (t, l..r);

pub Program : ast::Program = <stmts: StmtS*> => ast::Program { <> };

pub StmtS = Spanned<Stmt>;

pub Stmt : ast::Stmt = {
    VarStmt,
    PrintStmt,
    ExprStmt,
    ";" =>  ast::Stmt::Empty ,
}


pub VarStmt : ast::Stmt = {
    "var" <name: identifier> <initializer: ("=" <ExprS>)?> ";" => {
        ast::Stmt::Var( ast::Var {name, initializer} )
    }
};

pub PrintStmt : ast::Stmt = {
    "print" <ExprS> ";" => {
        ast::Stmt::Print(<>)
    }
}

pub ExprStmt : ast::Stmt = <ExprS> ";" => ast::Stmt::Expr(<>);

pub ExprS = Spanned<Expr>;

pub Expr = AssignExpr;

pub AssignExpr: ast::Expr = {
    <lhs: identifier> "=" <rhs: LogicOrExpr> => {
        let lhs = ast::Expr::Literal(ast::ExprLiteral::Ident(lhs));
        ast::Expr::Binary(ast::ExprBinary{
            lhs: Box::new(lhs),
            op: ast::OpInfix::Assign,
            rhs: Box::new(rhs),
        })
    },
    LogicOrExpr,
}

pub LogicOrExpr: ast::Expr = {
    <lhs: LogicOrExpr> "or" <rhs: LogicAndExpr> => {
        ast::Expr::Binary(ast::ExprBinary{
            lhs: Box::new(lhs),
            op: ast::OpInfix::Or,
            rhs: Box::new(rhs),
        })
    },
    LogicAndExpr,
}

pub LogicAndExpr: ast::Expr = {
    <lhs: LogicAndExpr> "and" <rhs: EqualityExpr> => {
        ast::Expr::Binary(ast::ExprBinary{
            lhs: Box::new(lhs),
            op: ast::OpInfix::And,
            rhs: Box::new(rhs),
        })
    },
    EqualityExpr,
}

pub EqualityExpr: ast::Expr = {
    <lhs: EqualityExpr> <op: EqualityOp> <rhs: CmpExpr> => {
        ast::Expr::Binary(ast::ExprBinary{
            lhs: Box::new(lhs),
            op,
            rhs: Box::new(rhs),
        })
    },
    CmpExpr,
}

pub EqualityOp: ast::OpInfix = {
    "==" => ast::OpInfix::Eq,
    "!=" => ast::OpInfix::Ne,
}

pub CmpExpr: ast::Expr = {
    <lhs: CmpExpr> <op: CmpOp> <rhs: TermExpr> => {
        ast::Expr::Binary(ast::ExprBinary{
            lhs: Box::new(lhs),
            op,
            rhs: Box::new(rhs),
        })
    },
    TermExpr,
}

pub CmpOp: ast::OpInfix = {
    ">" => ast::OpInfix::Gt,
    ">=" => ast::OpInfix::Ge,
    "<" => ast::OpInfix::Lt,
    "<=" => ast::OpInfix::Le,
}

pub TermExpr: ast::Expr = {
    <lhs: TermExpr> <op: ExprOp> <rhs: FactorExpr> => {
        ast::Expr::Binary(ast::ExprBinary{
            lhs: Box::new(lhs),
            op,
            rhs: Box::new(rhs),
        })
    },

    FactorExpr,
}

pub ExprOp: ast::OpInfix = {
    "+" => ast::OpInfix::Add,
    "-" => ast::OpInfix::Sub,
}

pub FactorExpr: ast::Expr = {
    <lhs: FactorExpr> <op:FactorOp> <rhs: UnaryExpr> => {
        ast::Expr::Binary(ast::ExprBinary{
            lhs: Box::new(lhs),
            op,
            rhs: Box::new(rhs),
        })
    },

    UnaryExpr
}

pub FactorOp: ast::OpInfix = {
    "*" => ast::OpInfix::Mul,
    "/" => ast::OpInfix::Div,
}

pub UnaryExpr: ast::Expr = {
    <op: UnaryOp> <rhs: PrimaryExpr> => {
        ast::Expr::Unary(ast::ExprUnary{
            op,
            rhs: Box::new(rhs),
        })
    },

    PrimaryExpr,
}

pub UnaryOp: ast::OpPrefix = {
    "-" => ast::OpPrefix::Neg,
    "!" => ast::OpPrefix::Not,
}

pub PrimaryExpr: ast::Expr =  {
    "nil" => ast::Expr::Literal(ast::ExprLiteral::Nil),
    number => ast::Expr::Literal(ast::ExprLiteral::Num(<>)),
    string => ast::Expr::Literal(ast::ExprLiteral::Str(<>)),
    "true" => ast::Expr::Literal(ast::ExprLiteral::Bool(true)),
    "false" => ast::Expr::Literal(ast::ExprLiteral::Bool(false)),
    identifier => ast::Expr::Literal(ast::ExprLiteral::Ident(<>)),
    
    // grouping
    "(" <Expr> ")",
}

extern {
    type Location = usize;
    type Error = SyntaxError;

    enum Token {
        // Single-character tokens.
        "(" => Token::LeftParen,
        ")" => Token::RightParen,
        "{" => Token::LeftBrace,
        "}" => Token::RightBrace,
        "," => Token::Comma,
        "." => Token::Dot,
        "-" => Token::Minus,
        "+" => Token::Plus,
        ";" => Token::Semicolon,
        "/" => Token::Slash,
        "*" => Token::Star,

        // One or two character tokens.
        "!" => Token::Bang,
        "!=" => Token::BangEqual,
        "=" => Token::Equal,
        "==" => Token::EqualEqual,
        ">" => Token::Greater,
        ">=" => Token::GreaterEqual,
        "<" => Token::Less,
        "<=" => Token::LessEqual,

        // Literals.
        identifier => Token::Ident(<String>),
        string => Token::Str(<String>),
        number => Token::Num(<f64>),

        // Keywords.
        "and" => Token::And,
        "class" => Token::Class,
        "else" => Token::Else,
        "false" => Token::False,
        "for" => Token::For,
        "fun" => Token::Fun,
        "if" => Token::If,
        "nil" => Token::Nil,
        "or" => Token::Or,
        "print" => Token::Print,
        "return" => Token::Return,
        "super" => Token::Super,
        "this" => Token::This,
        "true" => Token::True,
        "var" => Token::Var,
        "while" => Token::While,
    }
}
